#!/usr/bin/env coffee

Path = require 'path'
Fs   = require 'fs'

exit = (msg) ->
  console.log msg
  process.exit 1

mtime = (p) ->
  Fs.statSync(p).mtime.getTime()

downloadsDir = ->
  Path.join(process.env['HOME'], 'Downloads')

sortBy = (elts, fn) ->
  decorate = elts.map (e) -> [fn(e), e]
  decorate.sort().map (a) -> a[1]

listdirByMostRecent = (d) ->
  entries = Fs.readdirSync(d).map (p) -> Path.join(d, p)
  sortBy entries, mtime

main = ->
  dir = downloadsDir()
  exit "#{dir} doesn't exist" unless Fs.existsSync(dir)
  downloads = listdirByMostRecent(dir)
  exit "Nothing in #{dir}" if downloads.length is 0
  mostRecent = downloads[downloads.length - 1]
  base = Path.basename mostRecent
  exit "./#{base} already exists" if Fs.existsSync(base)
  Fs.renameSync mostRecent, base
  console.log "Moved #{base} to ."

main()
